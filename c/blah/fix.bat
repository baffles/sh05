@echo off

rem Borrowed from Allegro

rem Test if there are too many args.
if [%4] == []        goto arg3
goto help

:arg3
rem Test if third arg is ok.
if [%3] == [--quick]     goto arg2
if [%3] == [--msvcpaths] goto arg2
if [%3] == []            goto arg2
goto help

:arg2
rem Test if second arg is ok.
if [%2] == [--quick]     goto arg1
if [%2] == [--msvcpaths] goto arg1
if [%2] == []            goto arg1
goto help

:arg1
rem Test if first arg is ok.
if [%1] == [bcc32]   goto head
if [%1] == [djgpp]   goto head
if [%1] == [mingw32] goto head
if [%1] == [msvc]    goto head
if [%1] == [msvc7]   goto head
if [%1] == [icl]     goto head
if [%1] == [watcom]  goto head
goto help


:head
rem Generate header of makefile and alplatf.h,
rem then go to platform specific function.
echo # generated by fix.bat > makefile
echo # generated by fix.bat > libacc\makefile
echo /* generated by fix.bat */ > include\platf.h
echo /* generated by fix.bat */ > libacc\include\platf.h

if [%1] == [bcc32]   goto bcc32
if [%1] == [djgpp]   goto djgpp
if [%1] == [mingw32] goto mingw32
if [%1] == [msvc]    goto msvc6
if [%1] == [msvc7]   goto msvc7
if [%1] == [icl]     goto icl
if [%1] == [watcom]  goto watcom

echo fix.bat internal error: not reached
goto help

:bcc32
echo Configuring for Windows/BCC32...
echo MAKEFILE_INC = makefile.bcc >> makefile
echo MAKEFILE_INC = makefile.bcc >> libacc\makefile
echo #define PLAT_BCC32 >> include\platf.h
echo #define PLAT_BCC32 >> libacc\include\platf.h
goto tail

:djgpp
echo Configuring for DOS/djgpp...
echo MAKEFILE_INC = makefile.dj >> makefile
echo MAKEFILE_INC = makefile.dj >> libacc\makefile
echo #define PLAT_DJGPP >> include\platf.h
echo #define PLAT_DJGPP >> libacc\include\platf.h
goto tail

:mingw32
echo Configuring for Windows/Mingw32...
echo MAKEFILE_INC = makefile.mgw >> makefile
echo MAKEFILE_INC = makefile.mgw >> libacc\makefile
echo #define PLAT_MINGW32 >> include\platf.h
echo #define PLAT_MINGW32 >> libacc\include\platf.h
goto tail

:icl
echo Configuring for Windows/ICL...
echo COMPILER_ICL = 1 >> makefile
echo COMPILER_ICL = 1 >> libacc\makefile
goto msvccommon

:msvc7
echo Configuring for Windows/MSVC7...
echo COMPILER_MSVC7 = 1 >> makefile
echo COMPILER_MSVC7 = 1 >> libacc\makefile
goto msvccommon

:msvc6
echo Configuring for Windows/MSVC6...
goto msvccommon

:msvccommon
echo MAKEFILE_INC = makefile.vc >> makefile
echo MAKEFILE_INC = makefile.vc >> libacc\makefile
echo #define ALLEGRO_MSVC >> include\platf.h
echo #define ALLEGRO_MSVC >> libacc\include\platf.h
goto tail

:watcom
echo Configuring for DOS/Watcom...
echo MAKEFILE_INC = makefile.wat >> makefile
echo MAKEFILE_INC = makefile.wat >> libacc\makefile
echo #define ALLEGRO_WATCOM >> include\platf.h
echo #define ALLEGRO_WATCOM >> libacc\include\platf.h
goto tail

:help
echo.
echo Usage: fix platform [--quick] [--msvcpaths]
echo.
echo Where platform is one of: bcc32, djgpp, mingw32, msvc, msvc7, icl or watcom.
echo.
echo The --quick parameter is used to turn off LF to CR/LF conversion.
echo.
echo Use the --msvcpaths parameter if your MSVCDir variable contains 
echo spaces (you can view content of that variable by typing 
echo echo %%MSVCDir%%
echo on the command line). Remember that this will only work if you
echo have MinGW gcc in your PATH.
echo.
goto end

:convertmsvcdir
echo Converting MSVCDir path...
gcc -s -o msvchelp.exe misc/msvchelp.c
msvchelp MSVCDir
del msvchelp.exe
echo include makefile.helper >> makefile
echo include makefile.helper >> libacc\makefile
goto realtail

:tail

if [%3] == [--msvcpaths] goto convertmsvcdir
if [%2] == [--msvcpaths] goto convertmsvcdir

:realtail
rem Generate last line of makefile and optionally convert CR/LF.
echo include makefile.all >> makefile
echo include makefile.all >> libacc\makefile

if [%2] == [--quick] goto done
if [%3] == [--quick] goto done
if [%1] == [bcc32]   goto done
if [%1] == [mingw32] goto done
if [%1] == [msvc]    goto done
if [%1] == [msvc7]   goto done

echo Converting files to DOS CR/LF format...
utod .../*.bat .../*.sh .../*.c *.cfg .../*.h .../*.inc .../*.rc
utod .../*.rh .../*.inl .../*.s .../*.txt .../*._tx makefile.*

:done
echo Done!

:end
